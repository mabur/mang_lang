result@{
    input =
"Sabqponm
abcryxxl
accszExk
acctuvwj
abdefghi"

    count_alphabet = in c out sub!(number!c number!'a')

    heights = <>
    start = [nan nan]
    goal = [nan nan]
    lines = split!(newline input)
    y = 0
    while lines
        line = take!lines
        x = 0
        while line
            c = take!line
            start = is c 'S' then [x y] else start
            goal = is c 'E' then [x y] else goal
            height = is c
                'S' then count_alphabet!'a'
                'E' then count_alphabet!'z'
                else count_alphabet!c
            heights = put!(([x y] height) heights)
            line = drop!line
            x = inc!x
        end
        lines = drop!lines
        y = inc!y
    end
    
    visited = <start:yes>
    active_frontier = [start]
    steps = 0
    while not!get!(goal visited no)
        next_frontier = []
        while active_frontier
            position = take!active_frontier
            height = get!(position heights nan)                        
            x = first!position
            y = second!position
            neighbours = [[x inc!y] [x dec!y] [inc!x y] [dec!x y]]
            while neighbours
                neighbour = take!neighbours
                neighbour_height = get!(neighbour heights inf)
                height_difference = sub!(neighbour_height height)
                ok_neighbour = and?[
                    less?(height_difference 2)
                    not!get!(neighbour visited no)
                ]
                next_frontier = if ok_neighbour then put!(neighbour next_frontier)
                    else next_frontier
                visited = if ok_neighbour then put!((neighbour yes) visited)
                    else visited
                neighbours = drop!neighbours
            end
            active_frontier = drop!active_frontier
        end
        steps = inc!steps
        active_frontier = next_frontier
    end

    result = steps
}
