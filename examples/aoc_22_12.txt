result@{
    input =
"Sabqponm
abcryxxl
accszExk
acctuvwj
abdefghi"

    count_alphabet = in c out sub!(number!c number!'a')

    heights = <>
    start = [nan nan]
    goal = [nan nan]
    lines = split!(newline input)
    y = 0
    for line in lines
        x = 0
        for c in line
            start = is c 'S' then [x y] else start
            goal = is c 'E' then [x y] else goal
            height = is c
                'S' then count_alphabet!'a'
                'E' then count_alphabet!'z'
                else count_alphabet!c
            heights += ([x y] height)
            x = inc!x
        end
        y = inc!y
    end
    
    visited = <(start yes)>
    active_frontier = [start]
    steps = 0
    while not!get!(goal visited no)
        next_frontier = []
        for position in active_frontier
            height = heights!position                        
            x = position!0
            y = position!1
            neighbours = [[x inc!y] [x dec!y] [inc!x y] [dec!x y]]
            for neighbour in neighbours
                neighbour_height = get!(neighbour heights inf)
                height_difference = sub!(neighbour_height height)
                ok_neighbour = and?[
                    less?(height_difference 2)
                    not!get!(neighbour visited no)
                ]
                for _ in ok_neighbour                
                    next_frontier = put!(neighbour next_frontier)
                    visited = put!((neighbour yes) visited)
                end
            end
        end
        steps = inc!steps
        active_frontier = next_frontier
    end

    result = steps
}
