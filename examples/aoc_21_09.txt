risk_levels@{
    map = [
        "bbbbbbbbbbbb"
        "b2199943210b"
        "b3987894921b"
        "b9856789892b"
        "b8767896789b"
        "b9899965678b"
        "bbbbbbbbbbbb"
    ]
    parse_height = in character out
        is character
            'b' then 10
            else parse_digit!character
    height = count!map
    width = count!take!map
    num_low_points = 0
    risk_levels = 0
    y = 0
    while less?(y sub!(height 2))
        row_above = map!0
        row_middle = map!1
        row_below = map!2
        x = 0
        while less?(x sub!(width 2))
            point = parse_height!row_middle!1
            neighbours = [
                parse_height!row_above!1
                parse_height!row_middle!0
                parse_height!row_middle!2
                parse_height!row_below!1
            ]
            is_low_point = less?(point min_item!neighbours)
            num_low_points =
                if is_low_point then
                    inc!num_low_points
                else
                    num_low_points
            risk_levels =
                if is_low_point then
                    sum![risk_levels point 1]
                else
                    risk_levels
            row_above = drop!row_above
            row_middle = drop!row_middle
            row_below = drop!row_below
            x = inc!x
        end
        map = drop!map
        y = inc!y
    end
}
